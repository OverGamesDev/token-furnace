import "std/nft_collection_interface"
Contract TokenFurnace(
    nftTemplateId: ByteVec,
    collectionUri: ByteVec,
    mut totalSupply: U256
  ) implements INFTCollection {

   event Burned(caller: Address, tokenBurned: ByteVec, amount: U256, nft: Bool, timestamp: U256)

    enum ErrorCodes {
      IncorrectTokenIndex = 0
      NFTNotFound = 1
      NFTNotPartOfCollection = 2
      TokenIdNotAccepted = 4
      CannotBurnZero = 5
    }


   @using(assetsInContract = false, preapprovedAssets = true, checkExternalCaller = false)
   pub fn burn(tokenIdToBurn: ByteVec, amountToBurn: U256, withNft: Bool) -> (){
      let caller = callerAddress!()
      assert!(tokenIdToBurn != ALPH , ErrorCodes.TokenIdNotAccepted)
      assert!(amountToBurn > 0, ErrorCodes.CannotBurnZero)
      
      let tokenIdHex = convert(tokenIdToBurn)
      if(withNft){
       let _ =  mint{caller -> ALPH: minimalContractDeposit!()}(tokenIdHex, amountToBurn, caller)
      }

      emit Burned(caller, tokenIdHex, amountToBurn, withNft, blockTimeStamp!())
      burnToken!(caller, tokenIdToBurn, amountToBurn)
   }

   @using(preapprovedAssets = true, updateFields = true)
   fn mint(tokenIdBurned: ByteVec, amountBurned: U256, burnerCaller: Address) -> (ByteVec) {

     let (initialImmState, initialMutState) = BurnerNFT.encodeFields!(selfContractId!(), totalSupply, tokenIdBurned, amountBurned)
 
     let contractId = copyCreateSubContractWithToken!{burnerCaller -> ALPH: minimalContractDeposit!()}(
         toByteVec!(totalSupply),
         nftTemplateId,
         initialImmState,
         initialMutState,
         1,
         burnerCaller
     )
 
     totalSupply = totalSupply + 1
     return contractId
   }

    pub fn getCollectionUri() -> ByteVec {
        return collectionUri
    }
    
    pub fn totalSupply() -> U256 {
        return totalSupply
    }


    pub fn nftByIndex(index: U256) -> INFT {
        checkCaller!(index < totalSupply(), ErrorCodes.IncorrectTokenIndex)
    
        let nftTokenId = subContractId!(toByteVec!(index))
        assert!(contractExists!(nftTokenId), ErrorCodes.NFTNotFound)
    
        return INFT(nftTokenId)
    }

    pub fn validateNFT(nftId: ByteVec, nftIndex: U256) -> () {
        let expectedTokenContract =  nftByIndex(nftIndex)
        assert!(nftId == contractId!(expectedTokenContract), ErrorCodes.NFTNotPartOfCollection)
    }

    fn convert(array: ByteVec) -> ByteVec {
      let hexBytes = [b`0`, b`1`, b`2`, b`3`, b`4`, b`5`, b`6`, b`7`, b`8`, b`9`, b`a`, b`b`, b`c`, b`d`, b`e`, b`f`]
      let mut result = #
      for(let mut offset = 0; offset < size!(array); offset = offset + 1) {
         let byte = u256From1Byte!(byteVecSlice!(array, offset, offset + 1)) & 0xFF
         let high = (byte >> 4) & 0x0F
         let low = byte & 0x0F
         result = result ++ hexBytes[high] ++ hexBytes[low]
      }
  
      return result
    }

}
